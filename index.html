<!DOCTYPE html>
<html xmlns="http://www.w3.org/1999/xhtml" xml:lang="en" lang="en">
  <head>
    <meta content="text/html;charset=utf-8" http-equiv="Content-Type">
    <meta content="utf-8" http-equiv="encoding">
    <title>Oxford Model</title>
    <script src="agentscript/lib/agentscript.js"></script>
    <script src="agentscript/tools/coffee-script.js"></script>
    <script src="lib/jquery.js"></script>
    <script src="lib/jquery.flot.js"></script>
    <script src="lib/jquery.flot.categories.js"></script>
    <script type="text/coffeescript">
    # This model is Copyright 2014, Wybo Wiersma, available under the GPL v3
    # This is Epsteins model of protest
    u = ABM.util # ABM.util alias, u.s is also ABM.shapes accessor.
    log = (object) -> console.log object

    ABM.TYPE_STRINGS = ["Normal", "Enclave for the risk-taking",
      "Micro-contributions"]
    ABM.TYPES = {normal: 0, enclave: 1, micro: 2}

    class Model extends ABM.Model
      setup: ->
        @agentBreeds ["citizens", "cops"], Agent
        @citizenDensity = 0.7
        @copDensity = 0.02
        @size = 0.9
        @vision = {diamond: 7} # Neumann 7
        @type = @world.type
        #@vision = 7 # Neumann 7
        #@vision = 1
        
        # Shape to bitmap for better performance.
        @citizens.setUseSprites()
        @animator.setRate 20, false

        for patch in @patches.create()
          if @type is ABM.TYPES.enclave
            if patch.position.y > 0
              patch.color = u.randomGray(180, 204)
              console.log 'bigger'
            else
              patch.color = u.randomGray(234, 255)
              console.log 'smaller'
          else
            patch.color = u.randomGray(224, 255)

        space = @patches.length

        for citizen in @citizens.create @citizenDensity * space
          citizen.model = this # needs to be added to model initiation TODO see if needed
          citizen.vision = @vision
          citizen.size = @size
          citizen.shape = "person"
          citizen.setColor("green")
          citizen.moveToRandomEmptyLocation()

          citizen.regimeLegitimacy = 0.32
          citizen.threshold = 0.1
          citizen.thresholdMicro = 0.0
          citizen.hardship = u.randomFloat()
          citizen.riskAversion = u.randomFloat()
          citizen.active = false
          citizen.activeMicro = 0.0
          citizen.prisonSentence = 0

          citizen.grievance = ->
            @hardship * (1 - @regimeLegitimacy)

          citizen.arrestProbability = ->
            cops = 0
            actives = 1
      
            for agent in @neighbors(@vision)
              if agent.breed.name is "cops"
                cops += 1
              else
                if @model.type is ABM.TYPES.micro
                  if agent.breed.name is "citizen"
                    actives += agent.activeMicro
                else
                  if agent.breed.name is "citizen" and agent.active
                    actives += 1

            1 - Math.exp(-2.3 * cops / actives)

          citizen.netRisk = ->
            @arrestProbability() * @riskAversion

          citizen.imprison = (sentence) ->
            @prisonSentence = sentence
            @moveOff()

          citizen.imprisoned = ->
            @prisonSentence > 0

          citizen.activate = ->
            if @model.type is ABM.TYPES.micro
              activation = @grievance() - @netRisk()

              if activation > @threshold
                @active = true
                @setColor("red")
                @activeMicro = 1.0
              else if activation > @thresholdMicro
                @active = false
                @setColor([255, 164, 0])
                @activeMicro = 0.4
              else
                @active = false
                @setColor("green")
                @activeMicro = 0.0

            else
              if @grievance() - @netRisk() > @threshold
                @active = true
                @setColor("red")
              else
                @active = false
                @setColor("green")

          citizen.act = ->
            if @imprisoned()
              @prisonSentence -= 1
              if !@imprisoned() # just released
                @moveToRandomEmptyLocation()
            if !@imprisoned() # just released included
              empty = @randomEmptyNeighbor()
              if @model.type is ABM.TYPES.enclave
                if empty and (@riskAversion > 0.5 and
                    (empty.position.y > 0 or empty.position.y < 0 and
                      empty.position.y > @patch.position.y)) or
                    (@riskAversion < 0.5 and
                    (empty.position.y < 0 or empty.position.y > 0 and
                      empty.position.y < @patch.position.y))
                  empty = @randomEmptyNeighbor()

              @moveTo(empty.position) if empty
              @activate()

        for cop in @cops.create @copDensity * space
          cop.model = this
          cop.vision = @vision
          cop.size = @size
          cop.shape = "person"
          cop.setColor("blue")
          cop.moveToRandomEmptyLocation()
  
          cop.maxPrisonSentence = 30

          cop.makeArrest = ->
            protesters = 0
            passives = 0
            for agent in @neighbors(@vision)
              if agent.breed.name is "citizens" and agent.active
                protesters += 1
              else
                passives += 1

            protester = @neighbors(@vision).sample((agent) ->
              agent.breed.name is "citizens" and agent.active)
            if protester
              protester.imprison(1 + u.randomInt(@maxPrisonSentence))

          cop.act = ->
            empty = @randomEmptyNeighbor()
            @moveTo(empty.position) if empty
            @makeArrest()

        @ui = new UI(this, plotDiv: "#graph")

      prisoners: ->
        prisoners = []
        for citizen in @citizens
          if citizen.imprisoned()
            prisoners.push citizen
        prisoners

      actives: ->
        actives = []
        for citizen in @citizens
          if citizen.active and not citizen.imprisoned()
            actives.push citizen
        actives

      micros: ->
        micros = []
        for citizen in @citizens
          if !citizen.active and citizen.activeMicro > 0 and
              not citizen.imprisoned()
            micros.push citizen
        micros

      step: ->  # called by Model.animate
        @agents.shuffle()
        for agent in @agents
          agent.act()

        @ui.drawPlot(@animator.ticks)

        #@spriteSheet()

      spriteSheet: ->
        if @animator.draws is 2 # Show the sprite sheet if there is one after first draw
          sheet = u.last(u.s.spriteSheets) if u.s.spriteSheets.length isnt 0
          if sheet?
            log sheet
            document.getElementById("play").appendChild(sheet.canvas)

    class Agent extends ABM.Agent
      setColor: (color) ->
        if u.isString color
          @color = u.colorFromString(color)
        else
          @color = color
        @sprite = null

      moveToRandomEmptyLocation: ->
        @moveTo(@model.patches.sample((patch) -> patch.empty()).position)

      randomEmptyNeighbor: (type = ABM.TYPES.normal) ->
        @patch.neighbors(@vision).sample((patch) -> patch.empty())

    class UI
      constructor: (model, options = {}) ->
        @model = model
        @plotDiv = $(options.plotDiv)
        @plotOptions = {
          series: {
            shadowSize: 0
          } # faster without shadows
          xaxis: {
            show: false
          }
          yaxis: {
            min: 0
          }
        }
        @plotRioters = []
        @plotRioters.push({color: "green", data: []})
        @plotRioters.push({color: "red", data: []})
        @plotRioters.push({color: "black", data: []})
        @plotRioters.push({color: "blue", data: []})
        if @model.type is ABM.TYPES.micro
          @plotRioters.push({color: "orange", data: []})
        @resetPlot()

      resetPlot: ->
        @plotter = $.plot(@plotDiv, @plotRioters, @plotOptions)
        @drawPlot(0)

      drawPlot: (ticks) ->
        @plotRioters.data = []
        citizens = @model.citizens.length
        actives = @model.actives().length
        micros = @model.micros().length
        prisoners = @model.prisoners().length
        passives = citizens - actives - micros - prisoners
        cops = @model.cops.length
        @plotRioters[0].data.push [ticks, passives]
        @plotRioters[1].data.push [ticks, actives]
        @plotRioters[2].data.push [ticks, prisoners]
        @plotRioters[3].data.push [ticks, cops]
        if @model.type is ABM.TYPES.micro
          @plotRioters[4].data.push [ticks, micros]
        @plotter.setData(@plotRioters)
        @plotter.setupGrid()
        @plotter.draw()

    window.initialize = (type) ->
      window.model = new Model({
        div: "model",
        patchSize: 20,
        mapSize: 30
        isTorus: true,
        hasNeighbors: true,
        type: type
      })
      window.model.debug().once() # Debug: Put Model vars in global name space

    window.initializeFromSelector = (selector) ->
      key = selector.selectedIndex
      contexts = window.model.contexts
      for bull, context of contexts
        context.canvas.width = context.canvas.width
      window.initialize(key)

    setupDropdown = (option_select, options, default_option) ->
      i = 0
      for option in options
        $(option_select).append(
          '<option value="' + i + '"' + (default_option == i ? ' selected="selected"' : '') + '>' +
          options[i] + '</option>')
        i += 1

    setupDropdown('#mode_select', ABM.TYPE_STRINGS, model.type)
    window.initialize(ABM.TYPES.normal)
    
    </script>
  </head>
  <body>
    <div id="model"></div>
    <div id="play" style="float: left;"></div>
    <div id="graph" style="float: left; width: 825px; height: 500px;"></div>

    <h3>Usage</h3>
    <ul>
      <li><i>Start/Pause simulation</i> to run it</li>
      <li><i>Step generation</i> for stepwise</li>
    </ul>
    <p>
      <input type="button" id="start" value="Start/pause simulation" onclick="model.toggle();" /> 
      <input type="button" value="Step generation" onclick="model.once();" />
      <select id="mode_select" onchange="initializeFromSelector(this)"></select>
    </p>
  </body>
</html>
